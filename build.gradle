project.setDescription('Giac CAS for GeoGebra')

apply plugin: 'cpp'

def java_home = org.gradle.internal.jvm.Jvm.current().javaHome

// println java_home

model {
    platforms {
        win32 {
            architecture 'i386'
            operatingSystem 'windows'
        }
        win64 {
            architecture 'x64'
            operatingSystem 'windows'
        }
	linux_i686 { // this will be fixed in Gradle 2.4 (-> linux32)
            architecture 'i386'
            operatingSystem 'linux'
        }
	linux64 {
            architecture 'x64'
            operatingSystem 'linux'
        }
    }

    toolChains {
        mingw(Gcc) {
            target('win32') {
                cppCompiler.executable 'i686-w64-mingw32-g++'
                linker.executable 'i686-w64-mingw32-g++'
            }
            target('win64') {
                cppCompiler.executable 'x86_64-w64-mingw32-g++'
                linker.executable 'x86_64-w64-mingw32-g++'
            }
        }
    }
    components {
        // giac static libary
        giac(NativeLibrarySpec) {
            binaries.all{ 
                cppCompiler.define "GIAC_GGB"
                cppCompiler.define "IN_GIAC"
                cppCompiler.define "GIAC_GENERIC_CONSTANTS"
                cppCompiler.define "HAVE_UNISTD_H"
                cppCompiler.define "HAVE_LIBPTHREAD"
                cppCompiler.define "HAVE_SYSCONF"
                cppCompiler.define "HAVE_NO_HOME_DIRECTORY"
                cppCompiler.define "VERSION", '"1.1.3"'
                cppCompiler.define "TIMEOUT"
            }
        }

        icas(NativeExecutableSpec) {
            binaries.all {
                lib library: 'giac', linkage: 'static'
                linker.args '-lgmp', '-lpthread'
            }
        }

        javagiac(NativeLibrarySpec) {
            targetPlatform 'win32'
            targetPlatform 'win64'
	    targetPlatform 'linux_i686' // this will be fixed in Gradle 2.4 (-> linux32)
	    targetPlatform 'linux64'

            sources.cpp {
                source {
                    srcDirs 'src/giac/cpp', 'src/jni/cpp'
                    exclude '**/first.cc', '**/firstcc.cc', '**/hello.cc' // consider removing them from SVN
            	    }
                exportedHeaders {
                    srcDirs 'src/giac/headers', 'src/jni/jdkHeaders'
            	    }
        	}

            binaries.withType(SharedLibraryBinary) {
		// Common settings for all platforms and architectures:
                cppCompiler.define 'IN_GIAC'
                cppCompiler.define 'GIAC_GENERIC_CONSTANTS'
		// Additional settings can be defined in config.h.
		// They are automatically loaded if HAVE_CONFIG_H is defined below:
                cppCompiler.define 'HAVE_CONFIG_H'

                cppCompiler.args '-fexceptions'
                cppCompiler.args '-Os' // size optimization
                cppCompiler.args '-s' // stripping (maybe not needed)
                cppCompiler.args '-I.'

                linker.args '-s' // stripping
                linker.args "-I${java_home}/include", "-Isrc/jni/jdkHeaders"
		// Add libgcc and libstdc++ statically
		linker.args '-static-libgcc'
		linker.args '-static-libstdc++'

		// Architecture based settings:
                if (targetPlatform.architecture.name == 'i386') {
                    cppCompiler.define 'SMARTPTR64'
                    cppCompiler.define 'SIZEOF_LONG', '8'
            	    } else {
                    cppCompiler.define 'SIZEOF_LONG', '4'
            	    }

		// OS based settings:
                if (targetPlatform.operatingSystem.name == 'windows') {
                    cppCompiler.define 'GIAC_MPQS'
                    cppCompiler.define '__MINGW_H'
                    cppCompiler.define 'HAVE_NO_SYS_TIMES_H'
                    cppCompiler.define 'HAVE_NO_SYS_RESOURCE_WAIT_H'
                    cppCompiler.define 'HAVE_NO_PWD_H'
                    cppCompiler.define 'HAVE_NO_CWD'
                    cppCompiler.define 'usleep',''
                    cppCompiler.define 'YY_NO_UNISTD_H'

		    cppCompiler.args '-I', file('src/jni/jdkHeaders/win32').toString()
		    // Insert prebuilt libraries
            	    linker.args '-Wl,--add-stdcall-alias'
		    linker.args '-Wl,-Bstatic', '-lmpfr', '-lgmp'
		    linker.args '-L', "src/jni/prebuilt/windows/${targetPlatform.architecture.name}"
		    }

                if (targetPlatform.operatingSystem.name == 'linux') {
                    cppCompiler.define 'HAVE_UNISTD_H'

		    cppCompiler.args '-I', file('src/jni/jdkHeaders/linux').toString()
		    cppCompiler.args '-fno-strict-aliasing' // maybe not needed
		    cppCompiler.args '-DPIC' // maybe not needed

            	    if (targetPlatform.architecture.name == 'i686') {
			linker.args '-Wl,-Bstatic', '-lmpfr', '-lgmp'
            		} else {
			// 64 bit Linux versions require libgmp and libmpfr preinstalled:
			linker.args '-Wl,-Bdynamic', '-lmpfr', '-lgmp'
			// We cannot link them statically because we get always an error like
			// "relocation R_X86_64_32 against `.rodata.str1.1' can not be used when making a shared object; recompile with -fPIC"
			// TODO: try to fix this somehow.
			}
            	    }
        	}
    	    }
	}
    }

task run (dependsOn: 'icasExecutable', type: Exec) {
    description "Runs Giac's icas terminal"
    commandLine 'build/binaries/icasExecutable/icas'
    standardInput = System.in
    }
